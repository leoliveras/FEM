      SUBROUTINE BAKSUB
C***********************************************************************
C
C*** BACK-SUBSTITUTION ROUTINE
C
C***********************************************************************

      COMMON/UNIM1/NPOIN,NELEM,NBOUN,NLOAD,NPROP,NNODE,IINCS,IITER,
     *             KRESL,NCHEK,TOLER,NALGO,NSVAB,NDOFN,NINCS,NEVAB,
     *             NITER,NOUTP,FACTO,PVALU
     
      COMMON/UNIM2/PROPS(5,5),COORD(26),LNODS(25,2),IFPRE(52),
     *             FIXED(52),TLOAD(25,4),RLOAD(25,4),ELOAD(25,4),
     *             MATNO(25),STRES(25,2),PLAST(25),XDISP(52),
     *             TDISP(26,2),TREAC(26,2),ASTIF(52,52),ASLOD(52),
     *             REACT(52),FRESV(1352),PEFIX(52),ESTIF(4,4)
C
C     BACK-SUBSTITUTION ROUTINE
C
      NEQNS=NSVAB

      DO 10 IEQNS=1,NEQNS
       REACT(IEQNS)=0.0
 10   CONTINUE

      NEQN1=NEQNS+1
      DO 40 IEQNS=1,NEQNS
       NBACK=NEQN1-IEQNS
       PIVOT=ASTIF(NBACK,NBACK)
       RESID=ASLOD(NBACK)
       IF(NBACK.EQ.NEQNS) GO TO 30
       NBAC1=NBACK+1
       DO 20 ICOLS=NBAC1,NEQNS
        RESID=RESID-ASTIF(NBACK,ICOLS)*XDISP(ICOLS)
 20    CONTINUE
 30    IF(IFPRE(NBACK).EQ.0) XDISP(NBACK)=RESID/PIVOT
       IF(IFPRE(NBACK).EQ.1) XDISP(NBACK)=FIXED(NBACK)
       IF(IFPRE(NBACK).EQ.1) REACT(NBACK)=-RESID
 40   CONTINUE
      KOUNT=0
      DO 50 IPOIN=1,NPOIN
      DO 50 IDOFN=1,NDOFN
       KOUNT=KOUNT+1
       TDISP(IPOIN,IDOFN)=TDISP(IPOIN,IDOFN)+XDISP(KOUNT)
 50   TREAC(IPOIN,IDOFN)=TREAC(IPOIN,IDOFN)+REACT(KOUNT)

      DO 90 IPOIN=1,NPOIN
      DO 60 IELEM=1,NELEM
      DO 60 INODE=1,NNODE
       NLOCA=LNODS(IELEM,INODE)
 60   IF (IPOIN.EQ.NLOCA) GO TO 70
 70   DO 80 IDOFN=1, NDOFN
      NPOSN=(IPOIN-1)*NDOFN+IDOFN
      IEVAB=(INODE-1)*NDOFN+IDOFN
 80   TLOAD(IELEM,IEVAB)=TLOAD(IELEM,IEVAB)+REACT(NPOSN)
 90   CONTINUE
      RETURN
      END
    